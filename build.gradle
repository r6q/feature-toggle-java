plugins {
  id 'com.google.cloud.tools.jib' version '3.2.1'
  id 'org.springframework.boot' version '2.7.2'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
}

group = 'com.github.r6q'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
  compileTestJava {
    extendsFrom testAnnotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-web'

  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.4'

  implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

  implementation 'org.flywaydb:flyway-core'
  implementation 'org.flywaydb:flyway-mysql'

  implementation 'org.mapstruct:mapstruct:1.5.2.Final'

  implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
  implementation 'org.mybatis:mybatis:3.5.10' // https://github.com/mybatis/mybatis-3/pull/2477#issuecomment-1076916312

  implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'

  compileOnly 'org.projectlombok:lombok'

  runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

  annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
  annotationProcessor 'org.projectlombok:lombok'

  testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(JavaCompile).each {
  it.options.compilerArgs.add('--enable-preview')
}

test {
  jvmArgs += "--enable-preview"
  useJUnitPlatform {
    excludeTags("itest")
  }
}


def itest = tasks.register("itest", Test) {
  jvmArgs += "--enable-preview"
  group("verification")
  description("Runs the integration tests.")
  shouldRunAfter("test")
  useJUnitPlatform {
    includeTags("itest")
  }
}

tasks.named("check") {
  dependsOn(itest)
}
